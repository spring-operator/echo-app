buildscript {
	ext {
		springBootVersion = '1.2.8.RELEASE'
		echoVersion = '1.109.0'
		echoAppVersion = '0.0.1.BUILD-SNAPSHOT'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'spring-boot'

jar {
	baseName = 'echo-app'
	version = echoAppVersion
}

/**
 * No need to create your own Main. Simply use the one provided upstream.
 */
springBoot {
	mainClass = 'com.netflix.spinnaker.echo.Application'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenLocal()
	mavenCentral()
	jcenter()
	maven { url "http://spinnaker.bintray.com/gradle" }
}


dependencies {
	compile "com.netflix.spinnaker.echo:echo-web:${echoVersion}"
	compile('org.codehaus.groovy:groovy')
	testCompile('org.springframework.boot:spring-boot-starter-test')
}

/**
 * Let's pin everything to the version of Spring Boot selected at the top
 */
configurations.all {
	resolutionStrategy.eachDependency { DependencyResolveDetails details ->
		if (details.requested.group == 'org.springframework.boot') {
			details.useVersion springBootVersion
		}
	}


	exclude group: 'org.slf4j', module: 'slf4j-simple'
	exclude group: 'org.slf4j', module: 'slf4j-log4j12'
}

eclipse {
	classpath {
		 containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
		 containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
	}
}
